///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$#,##0.00;($#,##0.00)';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='M/D/YYYY';
SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';


LET vLastLoadStart = now();
LET vNoOfRows = 0;

LET vDateFormat = 'DD MMM YYYY';
LET vMonthFormat = 'MMM-YY';
///$tab MappingTable
Map_Debtor:
Mapping LOAD *
Inline
[
DebtorId, DebtorName
1, John Doe
2, Janet Smith
3, Joe King
4, Sara Desert
5, Mark Downes
]
;
///$tab GetData
Repayments:
LOAD
		*
		,Month([Repayment Date]) as Month
		,Year([Repayment Date]) as Year
		,date(MonthStart([Repayment Date]),'$(vMonthFormat)')	as [Month Year]
		,ApplyMap('Map_Debtor',DebtorID,'<Unknown>')	as [Debtor Name]
;
LOAD
		 1 as RepaymentCounter
		,date(Today()-Floor(rand()*1000),'$(vDateFormat)')	as [Repayment Date]
		,ceil(rand()/0.02) as [Repayment Day]
		,Round(rand()*10000, 0.01) as [Repayment Value]
		,ceil(rand()/0.05) as [DebtorID]
AutoGenerate (5000)
;


TotalLoan:
LOAD
		*
		,date([Loan Incepted],'$(vDateFormat)') as [Loan Incepted Date]
;
LOAD
		1 as LoanCounter
		,DebtorID
		,sum([Repayment Value]) as [Total Loan]
		,min([Repayment Date]) as [Loan Incepted]
Resident Repayments
Group by DebtorID
;

LET vNoOfRows = alt(vNoOfRows + NoOfRows('Population'),0);
///$tab Days Since Inception
//Build a table to accumulate  values for Days since Inception

//Find the longest duration
Temp_MaxDays:
LOAD
		max([Repayment Day])	as MaxDays
Resident Repayments
;

LET vMaxDays = peek('MaxDays', 0, 'Temp_MaxDays');


//Build the table
for vOuterLoop = 0 to vMaxDays
	for vInnerLoop = vOuterLoop to vMaxDays
		Accumulations:
		LOAD
			 $(vOuterLoop) as [Repayment Day]
			,$(vInnerLoop) as [Repayment Day Accumulated]
		AutoGenerate (1);
	NEXT
NEXT

DROP Table Temp_MaxDays;

LET vMaxDays =;
LET vOutLoop =;
LET vInnerLoop =;
///$tab MAT
//Build our MAT table
for vMAT = 0 to 11
	MAT:
	LOAD
		[Month Year]
		,date(AddMonths([Month Year], $(vMAT)),'$(vMonthFormat)') as [MAT Period]
	Resident Repayments
	where AddMonths([Month Year], $(vMAT)) < Today()
	;
NEXT
///$tab AverageBar
DebtorWithAvg:
LOAD
		 DebtorID
		,DebtorID as [DebtorID With Avg]
Resident Repayments
;

LOAD
		 DebtorID
		,'Avg' as [DebtorID With Avg]
Resident Repayments
;
///$tab End
LET vLastLoadEnd = now();